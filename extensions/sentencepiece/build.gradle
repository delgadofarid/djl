group "ai.djl.sentencepiece"

dependencies {
    api project(":api")

    testImplementation("org.testng:testng:${testng_version}") {
        exclude group: "junit", module: "junit"
    }

    testImplementation project(":pytorch:pytorch-engine")
    testImplementation "ai.djl.pytorch:pytorch-native-auto:${pytorch_version}"
    testImplementation "org.slf4j:slf4j-simple:${slf4j_version}"
}

compileJava.dependsOn(processResources)

processResources {
    outputs.dir file("${project.buildDir}/classes/java/main/native/lib")
    doLast {
        def url = "https://publish.djl.ai/sentencepiece-${sentencepiece_version}/jnilib/${djl_version}"
        def files = [
                "linux-x86_64": "libsentencepiece_native.so",
                "osx-x86_64"  : "libsentencepiece_native.dylib"
        ]
        def jnilibDir = "${project.projectDir}/jnilib/${djl_version}"
        files.each { entry ->
            def file = new File("${jnilibDir}/${entry.key}/${entry.value}")
            if (file.exists()) {
                project.logger.lifecycle("prebuilt or cached file found for ${entry.value}")
            } else {
                project.logger.lifecycle("Downloading ${url}/${entry.key}")
                file.getParentFile().mkdirs()
                def downloadPath = new URL("${url}/${entry.key}/${entry.value}")
                downloadPath.withInputStream { i -> file.withOutputStream { it << i } }
            }
        }
        copy {
            from jnilibDir
            into "${project.buildDir}/classes/java/main/native/lib"
        }

        // write properties
        def propFile = file("${project.buildDir}/classes/java/main/native/lib/sentencepiece.properties")
        propFile.text = "version=${sentencepiece_version}-${version}\n"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = "DJL NLP utilities for SentencePiece"
                description = "Deep Java Library (DJL) NLP utilities for SentencePiece"
                url = "http://www.djl.ai/sentencepiece/${project.name}"
            }
        }
    }
}

apply from: file("${rootProject.projectDir}/tools/gradle/cpp-formatter.gradle")

task compileJNI {
    doFirst {
        if (System.properties['os.name'].toLowerCase(Locale.ROOT).contains("mac")
                || System.properties['os.name'].toLowerCase(Locale.ROOT).contains("linux")) {
            exec {
                commandLine 'bash', 'build.sh'
            }

            // for ci to upload to S3
            def ciDir = "${project.projectDir}/jnilib/${djl_version}/"
            copy {
                from "${project.buildDir}/jnilib"
                into ciDir
            }
            delete System.getProperty("user.home") + "/.djl.ai/sentencepiece"
        } else {
            throw new IllegalStateException("Unknown Architecture " + System.properties['os.name'])
        }
    }
}

clean.doFirst {
    delete System.getProperty("user.home") + "/.djl.ai/sentencepiece"
}
